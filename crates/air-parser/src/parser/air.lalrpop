use crate::parser::ast::*;
use crate::parser::into_variable_and_path;
use crate::parser::lexer::LexerError;
use crate::parser::lexer::Token;

use lalrpop_util::ErrorRecovery;
use std::rc::Rc;

// the only thing why input matters here is just to introduce lifetime for Token
grammar<'err, 'input>(input: &'input str, errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, LexerError>>);

pub AIR = Instr;

Instr: Box<Instruction<'input>> = {
    "(" call <p:PeerPart> <f:FPart> <args:Args> <output:Output?> ")" => {
        let output = output.unwrap_or(CallOutput::None);
        let args = Rc::new(args);
        Box::new(Instruction::Call(Call{peer_part: p, function_part: f, args, output}))
    },

    "(" seq <l:Instr> <r:Instr> ")" => Box::new(Instruction::Seq(Seq(l, r))),
    "(" par <l:Instr> <r:Instr> ")" => Box::new(Instruction::Par(Par(l, r))),
    "(" null ")" => Box::new(Instruction::Null(Null)),

    "(" fold <iterable:Iterable> <iterator:Alphanumeric> <i:Instr> ")" => {
        let instruction = Rc::new(*i);
        Box::new(Instruction::Fold(Fold{ iterable, iterator, instruction }))
    },
    "(" next <i:Alphanumeric> ")" => Box::new(Instruction::Next(Next(i))),


    "(" xor <l:Instr> <r:Instr> ")" => Box::new(Instruction::Xor(Xor(l, r))),

    ! => { errors.push(<>); Box::new(Instruction::Error) },
}

Args: Vec<CallInstructionValue<'input>> = {
    "[" <args:(<Arg>)*> "]" => args
}

FPart: FunctionPart<'input> = {
    <f:Function> => FunctionPart::FuncName(f),
    "(" <sid:ServiceId> <f:Function> ")" => FunctionPart::ServiceIdWithFuncName(sid, f),
}

PeerPart: PeerPart<'input> = {
    <pid:PeerId> => PeerPart::PeerPk(pid),
    "(" <pid:PeerId> <sid:ServiceId> ")" => PeerPart::PeerPkWithServiceId(pid, sid),
}

Output: CallOutput<'input> = {
    <s:Alphanumeric> => CallOutput::Scalar(s),
    <a:Accumulator> => CallOutput::Accumulator(a),
};

Iterable: IterableValue<'input> = {
    <s:Alphanumeric> => IterableValue::Variable(s),
    <v:JsonPath> => {
        let (variable, path) = into_variable_and_path(v.0, v.1);
        IterableValue::JsonPath { variable, path }
    },
}

Function = CallInstructionValue;
PeerId = CallInstructionValue;
ServiceId = CallInstructionValue;
Arg = CallInstructionValue;

CallInstructionValue: CallInstructionValue<'input> = {
    <s:Literal> => CallInstructionValue::Literal(s),
    <s:Alphanumeric> => CallInstructionValue::Variable(s),
    <v:JsonPath> => {
        let (variable, path) = into_variable_and_path(v.0, v.1);
        CallInstructionValue::JsonPath { variable, path }
    },
    InitPeerId => CallInstructionValue::InitPeerId,
}

extern {
    type Location = usize;
    type Error = LexerError;

    enum Token<'input> {
        "(" => Token::OpenRoundBracket,
        ")" => Token::CloseRoundBracket,
        "[" => Token::OpenSquareBracket,
        "]" => Token::CloseSquareBracket,

        Alphanumeric => Token::Alphanumeric(<&'input str>),
        Literal => Token::StringLiteral(<&'input str>),
        JsonPath => Token::JsonPath(<&'input str>, <usize>),
        Accumulator => Token::Accumulator(<&'input str>),

        InitPeerId => Token::InitPeerId,

        call => Token::Call,
        seq => Token::Seq,
        par => Token::Par,
        null => Token::Null,
        fold => Token::Fold,
        xor => Token::Xor,
        next => Token::Next,
    }
}
